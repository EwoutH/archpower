# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=cldr-emoji-annotation
pkgver=34.0.0.0
_tag=${pkgver%.*}_${pkgver##*.}
pkgrel=1
pkgdesc="Emoji annotation files in CLDR"
arch=('any')
url="https://github.com/fujiwarat/cldr-emoji-annotation"
license=('custom')
source=("$pkgname-$pkgver.tar.gz::https://github.com/fujiwarat/cldr-emoji-annotation/archive/$_tag.tar.gz")
sha512sums=('277476677888e636ebece6abbc3eb7e1d9b89af79e2f498b814988b4cc10914a66edfa1dc3b39df6251939629f7a9b2a4f68e739dbceea37dba7d44ef1cdb62a')

build() {
  cd cldr-emoji-annotation-$_tag
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd cldr-emoji-annotation-$_tag
  make DESTDIR="$pkgdir" install

  install -Dm644 unicode-license.txt "$pkgdir"/usr/share/licenses/$pkgname/unicode-license.txt
}
