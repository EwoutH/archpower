# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libappimage
pkgver=0.1.10
pkgrel=1
pkgdesc="Reference implementation of the AppImage specification"
arch=(x86_64 powerpc64le)
url="https://appimage.org/"
license=(GPL)
depends=(cairo libarchive squashfuse)
makedepends=(cmake wget vim desktop-file-utils)
source=($pkgname-$pkgver.tar.gz::"https://github.com/AppImage/libappimage/archive/v$pkgver.tar.gz")
sha256sums=('5966991e573e3eec247accc3156a791f359afdaa4140db68c34d84f6ac8ea12b')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DUSE_SYSTEM_XZ=ON \
    -DUSE_SYSTEM_SQUASHFUSE=ON \
    -DUSE_SYSTEM_LIBARCHIVE=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
