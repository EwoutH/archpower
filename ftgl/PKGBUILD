# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>

pkgname=ftgl
_pkgver=2.4.0
pkgver=${_pkgver/-/}
pkgrel=10
pkgdesc="OpenGL library to use arbitrary fonts"
arch=(x86_64 powerpc64le)
url="https://sourceforge.net/projects/ftgl/"
license=(MIT)
depends=(freetype2 glu)
makedepends=(doxygen mesa graphviz freetype2-demos)
source=("http://ftp.de.debian.org/debian/pool/main/f/${pkgname}/${pkgname}_${_pkgver}.orig.tar.xz")
sha256sums=('e3e07a4b4aa5db98d69c621887001dc21b0cfce607868c0e0e4e37bcc7558676')

prepare() {
  cd $pkgname-${_pkgver}
  ./autogen.sh
  sed -i '/^SUBDIRS =/s/demo//' Makefile.in
  printf '%s\n' >>docs/doxygen.cfg.in \
    HAVE_DOT=yes DOT_IMAGE_FORMAT=svg INTERACTIVE_SVG=yes
}

build() {
  cd $pkgname-${_pkgver/-/\~}
  ./configure --build=$CHOST \
	      --prefix=/usr \
	      --with-pic \
	      --disable-static
  make
}

package() {
  cd $pkgname-${_pkgver/-/\~}
  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
